  Literal           ::=  [‘-’] integerLiteral
                      |  [‘-’] floatingPointLiteral
                      |  booleanLiteral
                      |  characterLiteral
                      |  stringLiteral
                      |  interpolatedString
                      |  symbolLiteral
                      |  ‘null’

  ids               ::=  id {‘,’ id}

  Path              ::=  StableId
                      |  [id ‘.’] ‘this’
  StableId          ::=  id
                      |  Path ‘.’ id
                      |  [id ‘.’] ‘super’ ‘.’ id

  InfixType         ::=  CompoundType {id [nl] CompoundType}
  CompoundType      ::=  SimpleType {‘with’ SimpleType}
  SimpleType        ::=  StableId
                      |  ‘Array’ ‘[’InfixType‘]’

  Expr             ::=  ‘if’ ‘(’ Expr ‘)’ {nl} Expr [[semi] ‘else’ Expr]
                      |  ‘while’ ‘(’ Expr ‘)’ {nl} Expr
                      |  ‘try’ Expr [‘catch’ Expr] [‘finally’ Expr]
                      |  ‘do’ Expr [semi] ‘while’ ‘(’ Expr ‘)’
                      |  ‘for’ (‘(’ Enumerators ‘)’ | ‘{’ Enumerators ‘}’) {nl} [‘yield’] Expr
                      |  ‘throw’ Expr
                      |  ‘return’ [Expr]
                      |  [SimpleExpr ‘.’] id ‘=’ Expr
                      |  SimpleExpr1 ArgumentExprs ‘=’ Expr

  PostfixExpr       ::=  InfixExpr [id [nl]]
  InfixExpr         ::=  PrefixExpr
                      |  InfixExpr id [nl] InfixExpr
					  |	 InfixExpr ‘match’ ‘{’ CaseClauses ‘}’
  PrefixExpr        ::=  [PrefixOperator] SimpleExpr
  PrefixOperator    ::=  ‘-’ | ‘+’ | ‘~’ | ‘!’
  SimpleExpr        ::=  ‘new’ (ClassTemplate | TemplateBody)
                      |  BlockExpr
                      |  SimpleExpr1
  SimpleExpr1       ::=  Literal
                      |  Path
                      |  ‘(’ [Exprs] ‘)’
                      |  SimpleExpr ‘.’ id
                      |  SimpleExpr1 ArgumentExprs -- вызов метод
  Exprs             ::=  Expr {‘,’ Expr}
  ArgumentExprs     ::=  ‘(’ [Exprs] ‘)’
                      |  [nl] BlockExpr
  BlockExpr         ::=  ‘{’ Block ‘}’
  Block             ::=  BlockStat {semi BlockStat} [Expr]
  BlockStat         ::=  VarDefs
                      |  Expr

  Enumerators       ::=  Generator {semi Generator}
  Generator         ::=  Pattern1 ‘<-’ Expr {[semi] Guard | semi Pattern1 ‘=’ Expr}

  CaseClauses       ::=  CaseClause { CaseClause }
  CaseClause        ::=  ‘case’ Pattern [Guard] ‘=>’ Block
  Guard             ::=  ‘if’ PostfixExpr

  Pattern           ::=  Pattern1 { ‘|’ Pattern1 }
  Pattern1          ::=  varid ‘:’ InfixType
                      |  Pattern2
  Pattern2          ::=  id
                      |  Pattern3
  Pattern3          ::=  SimplePattern
                      |  SimplePattern { id [nl] SimplePattern }
  SimplePattern     ::=  varid
                      |  Literal
                      |  StableId
                      |  StableId ‘(’ [Patterns] ‘)’
                      |  ‘(’ [Patterns] ‘)’
  Patterns          ::=  Pattern [‘,’ Patterns]

  FuncParamClause       ::=  [nl] ‘(’ [FuncParams] ‘)’
  FuncParams            ::=  FuncParam {‘,’ FuncParam}
  FuncParam             ::=  id [‘:’ InfixType] [‘=’ Expr]

  ClassParamClause  ::=  [nl] ‘(’ [ClassParams] ‘)’
  ClassParams       ::=  ClassParam {‘,’ ClassParam}
  ClassParam        ::=  {Modifier} [(‘val’ | ‘var’)]
                         id ‘:’ InfixType [‘=’ Expr]

  Modifier          ::=  LocalModifier
                      |  ‘private’
					  |	 ‘protected’
                      |  ‘override’
  LocalModifier     ::=  ‘abstract’
                      |  ‘final’
                      |  ‘sealed’

  TemplateBody      ::=  [nl] ‘{’ TemplateStat {semi TemplateStat} ‘}’
  TemplateStat      ::=  {Modifier} Def
                      |  {Modifier} Dcl
                      |  Expr
                      |

  Dcl               ::=  ‘val’ ValDcl
                      |  ‘var’ VarDcl
                      |  ‘def’ FunDcl

  ValDcl            ::=  ids ‘:’ InfixType
  VarDcl            ::=  ids ‘:’ InfixType
  FunDcl            ::=  FunSig [‘:’ InfixType]
  FunSig            ::=  id FuncParamClause

  VarDefs         	::=  ‘val’ Pattern2 {‘,’ Pattern2} [‘:’ InfixType] ‘=’ Expr
                      |  ‘var’ Pattern2 {‘,’ Pattern2} [‘:’ InfixType] ‘=’ Expr

  Def               ::=  VarDefs
                      |  ‘def’ FunDef
                      |  TmplDef

  FunDef            ::=  FunSig [‘:’ InfixType] ‘=’ Expr
                      |  FunSig [nl] ‘{’ Block ‘}’
                      |  ‘this’ FuncParamClause
                         (‘=’ ConstrExpr | [nl] ConstrBlock)

  TmplDef           ::=  ‘class’ ClassDef
                      |  ‘object’ ObjectDef
                      |  ‘trait’ TraitDef
  ClassDef          ::=  id
                         [ClassParamClause] ClassTemplateOpt
  TraitDef          ::=  id TraitTemplateOpt
  ObjectDef         ::=  id ClassTemplateOpt
  ClassTemplateOpt  ::=  ‘extends’ ClassTemplate | [[‘extends’] TemplateBody]
  TraitTemplateOpt  ::=  ‘extends’ TraitTemplate | [[‘extends’] TemplateBody]
  ClassTemplate     ::=  ClassParents [TemplateBody]
  TraitTemplate     ::=  TraitParents [TemplateBody]
  ClassParents      ::=  Constr {‘with’ SimpleType}
  TraitParents      ::=  SimpleType {‘with’ SimpleType}
  Constr            ::=  SimpleType [ArgumentExprs]

  ConstrExpr        ::=  SelfInvocation
                      |  ConstrBlock
  ConstrBlock       ::=  ‘{’ SelfInvocation {semi BlockStat} ‘}’
  SelfInvocation    ::=  ‘this’ ArgumentExprs

  TopStatSeq        ::=  TopStat {semi TopStat}
  TopStat           ::=  {Modifier} TmplDef