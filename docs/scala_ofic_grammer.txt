  Literal           ::=  [‘-’] integerLiteral
                      |  [‘-’] floatingPointLiteral
                      |  booleanLiteral
                      |  characterLiteral
                      |  stringLiteral
                      |  interpolatedString
                      |  symbolLiteral
                      |  ‘null’

  ids               ::=  id {‘,’ id}

  Path              ::=  StableId
                      |  [id ‘.’] ‘this’
  StableId          ::=  id
                      |  Path ‘.’ id
                      |  [id ‘.’] ‘super’ ‘.’ id

  InfixType         ::=  CompoundType {id [nl] CompoundType}
  CompoundType      ::=  SimpleType {‘with’ SimpleType}
  SimpleType        ::=  StableId
                      |  ‘Array’ ‘[’InfixType‘]’

  Expr             ::=  ‘if’ ‘(’ Expr ‘)’ {nl} Expr [[semi] ‘else’ Expr]
                      |  ‘while’ ‘(’ Expr ‘)’ {nl} Expr
                      |  ‘try’ Expr [‘catch’ Expr] [‘finally’ Expr]
                      |  ‘do’ Expr [semi] ‘while’ ‘(’ Expr ‘)’
                      |  ‘for’ (‘(’ Enumerators ‘)’ | ‘{’ Enumerators ‘}’) {nl} [‘yield’] Expr
                      |  ‘throw’ Expr
                      |  ‘return’ [Expr]
                      |  [SimpleExpr ‘.’] id ‘=’ Expr
                      |  SimpleExpr1 ArgumentExprs ‘=’ Expr -- всегда преобразование к вызову update метода
                      |  Path ArgumentExprs ‘=’ Expr -- всегда преобразование к вызову update метода
                      |  ‘(’ [Expr] ‘)’ ArgumentExprs ‘=’ Expr -- всегда преобразование к вызову update метода
                      |  SimpleExpr ‘.’ id ArgumentExprs ‘=’ Expr -- всегда преобразование к вызову update метода
                      |  InfixExpr

  PostfixExpr       ::=  InfixExpr [id [nl]]
  InfixExpr         ::=  PrefixExpr
                      |  InfixExpr id [nl] InfixExpr

  PrefixExpr        ::=  [PrefixOperator] SimpleExpr
  PrefixOperator    ::=  ‘-’ | ‘+’ | ‘~’ | ‘!’
  SimpleExpr        ::=  ‘new’ Constr
                      |  BlockExpr
                      |  SimpleExpr1
  SimpleExpr1       ::=  Literal
                      |  Path ArgumentExprs -- вызов метод
                      |  SimpleExpr ‘.’ id ArgumentExprs -- вызов метод
                      |  Path
                      |  ‘(’ [Expr] ‘)’
                      |  SimpleExpr ‘.’ id
  Exprs             ::=  Expr {‘,’ Expr}
  ArgumentExprs     ::=  ‘(’ [Exprs] ‘)’
  BlockExpr         ::=  ‘{’ Block ‘}’
  Block             ::=  BlockStat {semi BlockStat} [Expr]
  BlockStat         ::=  VarDefs
                      |  Expr

  Enumerators       ::=  Generator {semi Generator}
  Generator         ::=  id [‘:’ InfixType] ‘<-’ Expr {[semi] Guard | semi id [‘:’ InfixType] ‘=’ Expr}
  Guard             ::=  ‘if’ PostfixExpr

  FuncParamClause       ::=  [nl] ‘(’ [FuncParams] ‘)’
  FuncParams            ::=  FuncParam {‘,’ FuncParam}
  FuncParam             ::=  id [‘:’ InfixType] [‘=’ Expr]

  ClassParamClause  ::=  [nl] ‘(’ [ClassParams] ‘)’
  ClassParams       ::=  ClassParam {‘,’ ClassParam}
  ClassParam        ::=  {Modifier} [(‘val’ | ‘var’)]
                         id ‘:’ InfixType [‘=’ Expr]

  Modifier          ::=  LocalModifier
                      |  ‘private’
					  |	 ‘protected’
                      |  ‘override’
  LocalModifier     ::=  ‘abstract’
                      |  ‘final’
                      |  ‘sealed’

  TemplateBody      ::=  [nl] ‘{’ TemplateStat {semi TemplateStat} ‘}’
  TemplateStat      ::=  {Modifier} Def
                      |  {Modifier} Dcl
                      |  Expr
                      |

  Dcl               ::=  ‘val’ ValDcl
                      |  ‘var’ VarDcl
                      |  ‘def’ FunDcl

  ValDcl            ::=  ids ‘:’ InfixType
  VarDcl            ::=  ids ‘:’ InfixType
  FunDcl            ::=  FunSig [‘:’ InfixType]
  FunSig            ::=  id FuncParamClause

  VarDefs         	::=  ‘val’ id {‘,’ id} [‘:’ InfixType] ‘=’ Expr
                      |  ‘var’ id {‘,’ id} [‘:’ InfixType] ‘=’ Expr

  Def               ::=  VarDefs
                      |  ‘def’ FunDef
                      |  TmplDef

  FunDef            ::=  FunSig [‘:’ InfixType] ‘=’ Expr
                      |  FunSig [nl] ‘{’ Block ‘}’
                      |  ‘this’ FuncParamClause
                         (‘=’ ConstrExpr | [nl] ConstrBlock)

  TmplDef           ::=  ‘class’ ClassDef
                      |  ‘object’ ObjectDef
                      |  ‘trait’ TraitDef
  ClassDef          ::=  id
                         [ClassParamClause] ClassTemplateOpt
  TraitDef          ::=  id TraitTemplateOpt
  ObjectDef         ::=  id ClassTemplateOpt
  ClassTemplateOpt  ::=  ‘extends’ ClassTemplate | [[‘extends’] TemplateBody]
  TraitTemplateOpt  ::=  ‘extends’ TraitTemplate | [[‘extends’] TemplateBody]
  ClassTemplate     ::=  ClassParents [TemplateBody]
  TraitTemplate     ::=  TraitParents [TemplateBody]
  ClassParents      ::=  Constr {‘with’ SimpleType}
  TraitParents      ::=  SimpleType {‘with’ SimpleType}
  Constr            ::=  SimpleType [ArgumentExprs]

  ConstrExpr        ::=  SelfInvocation
                      |  ConstrBlock
  ConstrBlock       ::=  ‘{’ SelfInvocation {semi BlockStat} ‘}’
  SelfInvocation    ::=  ‘this’ ArgumentExprs

  TopStatSeq        ::=  TopStat {semi TopStat}
  TopStat           ::=  {Modifier} TmplDef